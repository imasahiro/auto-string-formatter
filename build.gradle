buildscript {
    repositories {
        maven { url "https://plugins.gradle.org/m2/" }
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath 'com.novoda:bintray-release:0.8.0'
        classpath 'me.champeau.gradle:jmh-gradle-plugin:0.4.5'
        classpath 'net.ltgt.gradle:gradle-apt-plugin:0.15'
        classpath 'pl.allegro.tech.build:axion-release-plugin:1.9.0'
    }
}

allprojects {
    repositories {
        jcenter()
    }

    apply plugin: 'java'
    apply plugin: 'checkstyle'
    apply plugin: 'eclipse'
    apply plugin: 'idea'
    apply plugin: 'maven'
    apply plugin: 'net.ltgt.apt-idea'
    apply plugin: 'net.ltgt.apt-eclipse'
    apply plugin: 'pl.allegro.tech.build.axion-release'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    def checkstyleConfigDir = "${rootProject.projectDir}/settings/checkstyle"
    checkstyle {
        configFile = new File("${checkstyleConfigDir}/checkstyle.xml")
        configProperties = ['checkstyleConfigDir': "$checkstyleConfigDir"]
        toolVersion = '8.8'
    }

    task checkstyle(group: 'Verification',
            description: 'Runs the checkstyle rules.',
            dependsOn: [tasks.checkstyleMain, tasks.checkstyleTest])

    test {
        dependsOn tasks.checkstyle
    }

    scmVersion {
        checks {
            uncommittedChanges = true
            aheadOfRemote = true
        }
        tag {
            prefix = 'string-formatter'
        }
    }
    project.version = scmVersion.version
}
